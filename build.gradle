plugins {
    id 'java'
    id 'java-gradle-plugin'
    id 'maven-publish'
}

group 'run.halo.gradle'
version '0.0.2-SNAPSHOT'
sourceCompatibility = JavaVersion.VERSION_17

repositories {
    mavenCentral()
}

publishing {
    repositories {
        maven {
            name = "SonatypeSnapShotsPackages"
            url = uri("https://s01.oss.sonatype.org/content/repositories/snapshots/")
            credentials {
                username = project.findProperty("ossr.user") ?: System.getenv("OSSR_USERNAME")
                password = project.findProperty("ossr.password") ?: System.getenv("OSSR_PASSWORD")
            }
        }
    }
    publications {
        ossr(MavenPublication) {
            from(components.java)
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
        }
    }
}

gradlePlugin {
    plugins {
        pluginDevelopment {
            id = 'run.halo.plugin-development'
            displayName = "Halo Gradle Plugin"
            description = "Halo Gradle Plugin For Plugin Development"
            implementationClass = 'run.halo.gradle.PluginDevelopmentPlugin'
        }
    }
}

ext {
    jackjson = '2.14.0'
    lang3 = '3.12.0'
    semver4j = '3.1.0'
    lombok = '1.18.24'
    asm = '9.4'
}

dependencies {
    // https://mvnrepository.com/artifact/com.github.docker-java/docker-java-transport-httpclient5
    implementation 'com.github.docker-java:docker-java-transport-httpclient5:3.2.14'
    implementation 'com.github.docker-java:docker-java-core:3.2.14'

    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$jackjson"
    implementation "com.fasterxml.jackson.core:jackson-databind:$jackjson"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jackjson"
    implementation "org.apache.commons:commons-lang3:$lang3"
    implementation "com.vdurmont:semver4j:$semver4j"
    implementation "org.ow2.asm:asm:$asm"
    implementation "org.ow2.asm:asm-commons:$asm"
    implementation 'commons-io:commons-io:2.11.0'
    compileOnly "org.projectlombok:lombok:$lombok"
    annotationProcessor "org.projectlombok:lombok:$lombok"

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
}

test {
    useJUnitPlatform()
}
