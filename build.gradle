plugins {
    id 'java'
    id 'java-gradle-plugin'
    id 'maven-publish'
}

group 'io.github.guqing.plugin'
version '0.0.4-SNAPSHOT'

repositories {
    mavenCentral()
}

publishing {
    repositories {
        maven {
            name = "SonatypeSnapShotsPackages"
            url = uri("https://s01.oss.sonatype.org/content/repositories/snapshots/")
            credentials {
                username = project.findProperty("ossr.user") ?: System.getenv("OSSR_USERNAME")
                password = project.findProperty("ossr.password") ?: System.getenv("OSSR_PASSWORD")
            }
        }
    }
    publications {
        ossr(MavenPublication) {
            from(components.java)
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
        }
    }
}

gradlePlugin {
    plugins {
        pluginDevelopment {
            id = 'io.github.guqing.plugin-development'
            implementationClass = 'io.github.guqing.plugin.PluginDevelopment'
        }
    }
}

ext {
    jackjson = '2.13.3'
    jacksonYaml = '2.8.3'
    lang3 = '3.12.0'
    semver4j = '3.1.0'
    lombok = '1.18.22'
}

dependencies {
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$jacksonYaml"
    implementation "com.fasterxml.jackson.core:jackson-databind:$jackjson"
    implementation "org.apache.commons:commons-lang3:$lang3"
    implementation "com.vdurmont:semver4j:$semver4j"
    implementation 'org.ow2.asm:asm:9.1'
    implementation 'org.ow2.asm:asm-commons:9.1'
    compileOnly "org.projectlombok:lombok:$lombok"
    annotationProcessor "org.projectlombok:lombok:$lombok"

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
}

test {
    useJUnitPlatform()
}
